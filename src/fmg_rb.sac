/**
 * This solves
 *  u_xx + u_yy = f
 * on Omega = [0, 1] x [0, 1]
 * with periodic boundary conditions.
 *
 * The technique uses a Gauss-Seidel update with successive overrelaxation,
 * red-black ordering.
 **/

use StdIO: all;
use Math: all;
use Array: all;
use Benchmarking: all;

double f(double x, double y)
{
  return -2d * sin(x + y);
}

double u(double x, double y)
{
  return sin(x + y);
}

/* Interpolation */
inline
double[m, m2], double[m, m2] 
prolongate_rb(double[n, n2] x_red, double[n, n2] x_black)
{
#if 0
  x = from_red_black(x_red, x_black);
  black = with {
                ([0, 0] <= [i, j] < [2 * n, n] step[2, 1]): x[i / 2, j];
                ([1, 0] <= [i, j] < [2 * n, n] step[2, 1]): 0d;
               }: genarray([2 * n, n], 0d);
  red = with {}: genarray([2 * n, n], 0d);

  res_red, res_black = stencil_rb(red, black, [1d, 1d / 2d, 1d / 4d]);
  return (res_red, res_black);
#else
  x = with {
            ([0, 0] <= [i, j] < [n, n] step [2, 2]): x_black[i, j / 2];
            ([0, 1] <= [i, j] < [n, n] step [2, 2]): x_red  [i, j / 2];
            ([1, 0] <= [i, j] < [n, n] step [2, 2]): x_red  [i, (n - j - 1) / 2];
            ([1, 1] <= [i, j] < [n, n] step [2, 2]): x_black[i, (n - j) / 2];
           }: genarray([n, n], 0d);

  res_black = with {
                ([0, 0] <= [i, j] < [2 * n, n] step [2, 1]):
                    x[i / 2, j];
                ([1, 0] <= [i, j] < [2 * n, n] step [2, 1]):
                    0.25 * (x[mod([i / 2    , n - 1 - j], [n, n])] +
                            x[mod([i / 2    , n     - j], [n, n])] +
                            x[mod([i / 2 + 1, n - 1 - j], [n, n])] +
                            x[mod([i / 2 + 1, n     - j], [n, n])]);
              }: genarray([2 * n, n], 0d);

  res_red   = with {
                ([0, 0] <= [i, j] < [2 * n, n] step [2, 1]):
                    0.5 * (x[mod([i / 2, j        ], [n, n])] +
                           x[mod([i / 2, j + 1    ], [n, n])]);
                ([1, 0] <= [i, j] < [2 * n, n] step [2, 1]):
                    0.5 * (x[mod([i / 2    , n - 1 - j], [n, n])] +
                           x[mod([i / 2 + 1, n - 1 - j], [n, n])]);
              }: genarray([2 * n, n], 0d);

  return (res_red, res_black);
#endif
}

/* Full-weighting operator: for P the interpolation matrix and d the
 * dimension, restrict is P^t / 2^d. */ 
inline
double[n2, m2], double[n2, m2]
restrict_rb(double[n, m] x_red, double[n, m] x_black)
{
#if 0
  res_red, res_black = stencil_rb(x_red, x_black, [1d, 1d / 2d, 1d / 4d] / 4d);
  y_red, y_black = to_red_black({[i, j] -> res_black[2 * i, j] 
                                        | [i, j] < [n / 2, n / 2]});
  return (y_red, y_black);
#else
  res_black = (1d * x_black +
              0.5 * {[i, j] -> x_red[mod([i - 1, m - 1 - j], [n, m])] +
                               x_red[mod([i    , j        ], [n, m])] +
                               x_red[mod([i    , j - 1    ], [n, m])] +
                               x_red[mod([i + 1, m - 1 - j], [n, m])]
                            | [i, j] < [n, m]} +
              0.25 * {[i, j] -> x_black[mod([i - 1, m - 1 - j], [n, m])] +
                                x_black[mod([i - 1, m     - j], [n, m])] +
                                x_black[mod([i + 1, m - 1 - j], [n, m])] +
                                x_black[mod([i + 1, m     - j], [n, m])]
                             | [i, j] < [n, m]}) / 4d;

  y = {[i, j] -> res_black[2 * i, j] 
              | [i, j] < [n / 2, n / 2]};

  y_red, y_black = to_red_black(y);
  
  return (y_red, y_black);
#endif
}

double[n, n2], double[n, n2] to_red_black(double[n, n] x)
  | (n2 == n / 2)
{
  black = {[i, j] -> x[i, 2 * j] 
                  | [0, 0] <= [i, j] < [n, n / 2] step [2, 1];
           [i, j] -> x[i, n - 2 * j - 1] 
                  | [1, 0] <= [i, j] < [n, n / 2] step [2, 1]};

  red   = {[i, j] -> x[i, 2 * j + 1]
                  | [0, 0] <= [i, j] < [n, n / 2] step [2, 1];
           [i, j] -> x[i, n - 2 * (j + 1)] 
                  | [1, 0] <= [i, j] < [n, n / 2] step [2, 1]};

  return (red, black);
}

double[n, n] from_red_black(double[n, n2] red, double[n, n2] black)
  | (n2 == n / 2)
{
  return with {
            ([0, 0] <= [i, j] < [n, n] step [2, 2]): black[i, j / 2];
            ([0, 1] <= [i, j] < [n, n] step [2, 2]): red  [i, j / 2];
            ([1, 0] <= [i, j] < [n, n] step [2, 2]): red  [i, (n - j - 1) / 2];
            ([1, 1] <= [i, j] < [n, n] step [2, 2]): black[i, (n - j) / 2];
         }: genarray([n, n], 0d);
}

/**
 * Weights are Manhattan distance to the central point, so equivalent to
 * weights [w[1], w[0], w[1]] in the more general case.
 **/
inline
double[n, n2], double[n, n2] 
stencil_rb(double[n, n2] x_red, double[n, n2] x_black, double[2] w)
{
  res_black = w[0] * x_black +
              w[1] * {[i, j] -> x_red[mod([i - 1, n2 - 1 - j], [n, n2])] +
                                x_red[mod([i    , j         ], [n, n2])] +
                                x_red[mod([i    , j  - 1    ], [n, n2])] +
                                x_red[mod([i + 1, n2 - 1 - j], [n, n2])]
                              | [i, j] < [n, n2]};

  res_red   = w[0] * x_red +
              w[1] * {[i, j] -> x_black[mod([i - 1, n2 - 1 - j], [n, n2])] +
                                x_black[mod([i    , j         ], [n, n2])] +
                                x_black[mod([i    , j  + 1    ], [n, n2])] +
                                x_black[mod([i + 1, n2 - 1 - j], [n, n2])]
                              | [i, j] < [n, n2]};
 
  return (res_red, res_black);
}

inline
double[n, n2], double[n, n2] 
stencil_rb(double[n, n2] x_red, double[n, n2] x_black, double[3] w)
{
  res_black = w[0] * x_black +
              w[1] * {[i, j] -> x_red[mod([i - 1, n2 - 1 - j], [n, n2])] +
                                x_red[mod([i    , j         ], [n, n2])] +
                                x_red[mod([i    , j  - 1    ], [n, n2])] +
                                x_red[mod([i + 1, n2 - 1 - j], [n, n2])]
                              | [i, j] < [n, n2]} +
              w[2] * {[i, j] -> x_black[mod([i - 1, n2 - 1 - j], [n, n2])] +
                                x_black[mod([i - 1, n2     - j], [n, n2])] +
                                x_black[mod([i + 1, n2 - 1 - j], [n, n2])] +
                                x_black[mod([i + 1, n2     - j], [n, n2])]
                              | [i, j] < [n, n2]};

  res_red   = w[0] * x_red +
              w[1] * {[i, j] -> x_black[mod([i - 1, n2 - 1 - j], [n, n2])] +
                                x_black[mod([i    , j         ], [n, n2])] +
                                x_black[mod([i    , j  + 1    ], [n, n2])] +
                                x_black[mod([i + 1, n2 - 1 - j], [n, n2])]
                              | [i, j] < [n, n2]} +
              w[2] * {[i, j] -> x_red[mod([i - 1, n2 - 1 - j], [n, n2])] +
                                x_red[mod([i - 1, n2 - 2 - j], [n, n2])] +
                                x_red[mod([i + 1, n2 - 1 - j], [n, n2])] +
                                x_red[mod([i + 1, n2 - 2 - j], [n, n2])]
                             | [i, j] < [n, n2]};
 
  return (res_red, res_black);
}

double L2_rb(double[d:shp] x, double[d:shp] y)
{
  return sqrt(sum(x * x) + sum(y * y));
}

inline
double[n, m], double[n, m] sor(double[n, m] u_red, double[n, m] u_black,
                               double[n, m] f_red, double[n, m] f_black,
                               double h, double omega)
  | (n == 2 * m && m % 2 == 0)
{
  update_red = {[i, j] -> u_red[mod([i - 1, m - 1 - j], [n, m])] +
                          u_red[mod([i    , j        ], [n, m])] +
                          u_red[mod([i    , j - 1    ], [n, m])] +
                          u_red[mod([i + 1, m - 1 - j], [n, m])]
                       | [i, j] < [n, m]};

  u_black = (1d - omega) * u_black +
            omega / 4d * (update_red - h * h * f_black);

  update_black = {[i, j] -> u_black[mod([i - 1, m - 1 - j], [n, m])] +
                            u_black[mod([i    , j        ], [n, m])] +
                            u_black[mod([i    , j + 1    ], [n, m])] +
                            u_black[mod([i + 1, m - 1 - j], [n, m])]
                         | [i, j] < [n, m]};

  u_red = (1d - omega) * u_red +
          omega / 4d * (update_black - h * h * f_red);

  return (u_red, u_black);
}

inline
double[n, n2], double[n, n2] 
sor_solve(double[n, n2] F_red, double[n, n2] F_black, 
          double h, double eps, int max_iter)
{
  U_red   = {iv -> 0d | iv < [n, n2]};
  U_black = {iv -> 0d | iv < [n, n2]};
  pi      = 4d * atan(1d);
  omega   = 2d / (1d + sin(pi * h));
  error   = 1e308;
  t       = 0;
  while (error > eps && t < max_iter) {
    U_red, U_black = sor(U_red, U_black, F_red, F_black, h, omega);
    Au_red, Au_black = stencil_rb(U_red, U_black, [-4d, 1d] / (h * h));
    error = L2_rb(F_red - Au_red, F_black - Au_black) /
              L2_rb(F_red, F_black);
    t++;
  }
  return (U_red, U_black);
}

inline
double[m, n], double[m, n] zero_rb(int m, int n)
{
  zero = {iv -> 0d | iv < [m, n]};
  return (zero, zero);
}

inline
double[n, n2] , double[n, n2] 
v_cycle(double[n, n2] U_red, double[n, n2] U_black,
        double[n, n2] F_red, double[n, n2] F_black, double h)
{
  if (n <= 128) {
    U_red, U_black = sor_solve(F_red, F_black, h, h * h, 10000);
  } else {
    for (i = 0; i < 2; i++) {
      U_red, U_black = sor(U_red, U_black, F_red, F_black, h, 2d / 3d);
    }

    Au_red, Au_black = stencil_rb(U_red, U_black, [-4d, 1d] / (h * h));
    r_red = Au_red - F_red;
    r_black = Au_black - F_black;
    r2h_red, r2h_black = restrict_rb(r_red, r_black);
    error2h_red, error2h_black = zero_rb(n / 2, n2 / 2);
    error2h_red, error2h_black = v_cycle(error2h_red, error2h_black,
                                         r2h_red, r2h_black, 2d * h);
    error_red, error_black = prolongate_rb(error2h_red, error2h_black);
    U_red = U_red - error_red;
    U_black = U_black - error_black;

    for (i = 0; i < 2; i++) {
      U_red, U_black = sor(U_red, U_black, F_red, F_black, h, 2d / 3d);
    }
  }

  return (U_red, U_black);
}

inline
double[n, n2] , double[n, n2] 
fmg(double[n, n2] F_red, double[n, n2] F_black, double h)
{
    return (F_red, F_black);
}

int main()
{
  i_init = getInterval("init", 2);

  start(i_init);
  pi = 4d * atan(1d);
  a = 0d;
  b = 2d * pi;
  n = 16384;
  h = (b - a) / tod(n);

  F = {[i, j] -> f(a + tod(i) * h, a + tod(j) * h)
              | [i, j] < [n, n]};
  F_red, F_black = to_red_black(F);

  end(i_init);
  time, unit = returnResultUnit(i_init);
  printf("Initialisation took %lf %s\n", time, unit);

  i_v = getInterval("v", 2);
  start(i_v);
  U_red, U_black = zero_rb(n, n / 2);
  U_red, U_black = v_cycle(U_red, U_black, F_red, F_black, h);
  do {
    Au_red, Au_black = stencil_rb(U_red, U_black, [-4d, 1d] / (h * h));
    r_red = F_red - Au_red;
    r_black = F_black - Au_black;
    corr_red, corr_black = zero_rb(n, n / 2);
    corr_red, corr_black = v_cycle(corr_red, corr_black, r_red, r_black, h);
    U_red   = U_red + corr_red;
    U_black = U_black + corr_black;
    printf("Relative residual norm = %e\n", 
              L2_rb(r_red, r_black) / L2_rb(F_red, F_black));
  } while (L2_rb(r_red, r_black) / L2_rb(F_red, F_black) > h * h);
  end(i_v);

  time, unit = returnResultUnit(i_v);
  printf("Solving %lf GB took %lf %s.\n", 
         3d * 8d * tod(n) * tod(n) / 1e9, // U, F, r
         time,
         unit);

  Utrue = {[i, j] -> u(a + tod(i) * h, a + tod(j) * h)
                  | [i, j] < [n, n]};
  Utrue_red, Utrue_black = to_red_black(Utrue);
  printf("Actual relative error %e\n", 
            L2_rb(U_red - Utrue_red, U_black - Utrue_black) / 
              L2_rb(Utrue_red, Utrue_black));

  return 0;
}
