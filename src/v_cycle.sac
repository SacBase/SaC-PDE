/**
 * This solves
 *  u_xx + u_yy = f
 * on Omega = [0, 1] x [0, 1]
 * with periodic boundary conditions.
 *
 * The technique uses a Gauss-Seidel update with successive overrelaxation.
 * We use red-black ordering so we can parallelise it. This is not exactly the 
 * same as the sequential Gauss-Seidel, but the convergence is similar.
 **/

use StdIO: all;
use Math: all;
use Array: all;

double f(double x, double y)
{
  return -2d * sin(x + y);
}

double u(double x, double y)
{
  return sin(x + y);
}

inline
double[d:shp] stencil(double[d:shp] x, double[d:wshp] w)
{
  return {iv -> sum({jv -> w[jv] * x[mod(iv + jv - wshp / 2, shp)]})
             | iv < shp};
}

/**
 * For 3D, we would have four partitions, starting at 
 * [0, 0, 0], [0, 1, 0], [1, 0, 0], [1, 1, 0].
 **/
inline
double[n, m] stencil_even_odd(double[n, m] x, double[3, 3] we, double[3, 3] wo)
{
  return {iv -> sum({jv -> we[jv] * x[mod(iv + jv - shape(we) / 2, shape(x))]})
             | [0, 0] <= iv < [n, m] step [2, 1];
          iv -> sum({jv -> wo[jv] * x[mod(iv + jv - shape(wo) / 2, shape(x))]})
             | [1, 0] <= iv < shape(x) step [2, 1]};
}

double[n, n] five_point(double[n, n] U, double h)
{
  return stencil(U, [[0d,  1d, 0d],
                     [1d, -4d, 1d],
                     [0d,  1d, 0d]]) / (h * h);
}

/**
 * Generates a d-dimensional grid, where elements with Manhattan-distance 
 * i to the center have weights cs[i].
 **/
inline
double[d:shp] manhattan_weights(double[d1] cs)
| (d1 == d + 1)
{
  ones = genarray([d1 - 1], 1);
  return {iv -> cs[sum(abs(iv - ones))] | iv < 3 * ones};
}

inline
double[d:shpo] fine2coarse(double[d:shpi] r)
{
  return {iv -> r[2 * iv] | iv < shpi / 2};
}

inline
double[d:shpo] coarse2fine(double[d:shpi] z)
{
  return {iv -> z[iv / 2] | iv < 2 * shpi step [2, 2]};
}

/* Interpolation */
double[n2, m2] prolongate(double[n, m] x)
{
  return stencil(coarse2fine(x), manhattan_weights([1d, 1d / 2d, 1d / 4d])); 
}

/* Full-weighting operator: for P the interpolation matrix and d the
 * dimension, restrict is P^t / 2^d. */ 
double[n2, m2] restrict(double[n, m] x)
{
  return fine2coarse(stencil(x, 
                             manhattan_weights([1d, 1d / 2d, 1d / 4d]) / 4d));
}

double L2(double[d:shp] x)
{
  return sqrt(sum(x * x)) / tod(prod(shp));
}

inline
double[n, m], double[n, m] to_red_black(double[n, n] x)
{
  black = {[i, j] -> x[i, 2 * j    ] 
                   | [0, 0] <= [i, j] < [n, n / 2] step [2, 1];
           [i, j] -> x[i, 2 * j + 1] 
                   | [1, 0] <= [i, j] < [n, n / 2] step [2, 1]};
  red = {[i, j] -> x[i, 2 * j + 1] 
                 | [0, 0] <= [i, j] < [n, n / 2] step [2, 1];
         [i, j] -> x[i, 2 * j    ] 
                 | [1, 0] <= [i, j] < [n, n / 2] step [2, 1]};

  return (red, black);
}

noinline 
double[n, n] from_red_black(double[n, m] red, double[n, m] black)
{
  x = {[i, j] -> black[i, j / 2]
              | [0, 0] <= [i, j] < [n, n] step [2, 2];
       [i, j] -> black[i, j / 2]
              | [1, 1] <= [i, j] < [n, n] step [2, 2];
       [i, j] -> red[i, j / 2]
              | [0, 1] <= [i, j] < [n, n] step [2, 2];
       [i, j] -> red[i, j / 2]
              | [1, 0] <= [i, j] < [n, n] step [2, 2]};
  return x;
}

inline
double[n, m], double[n, m] sor(double[n, m] u_red, double[n, m] u_black,
                               double[n, m] f_red, double[n, m] f_black,
                               double h, double omega)
  | (n == 2 * m && m % 2 == 0)
{
  /** 
   * TODO: can we express this as a single stencil?
   **/
  w_left  = [[0d, 1d, 0d],
             [1d, 1d, 0d],
             [0d, 1d, 0d]];
  w_right = [[0d, 1d, 0d],
             [0d, 1d, 1d],
             [0d, 1d, 0d]];

  update_red = stencil_even_odd(u_red, w_left, w_right);
  u_black = (1d - omega) * u_black +
            omega / 4d * (update_red - h * h * f_black);

  update_black = stencil_even_odd(u_black, w_right, w_left);
  u_red = (1d - omega) * u_red +
          omega / 4d * (update_black - h * h * f_red);

  return (u_red, u_black);
}

double[n, n] sor(double[n, n] u, double[n, n] f, double h, double omega)
{
  u_red, u_black = to_red_black(u); 
  f_red, f_black = to_red_black(f); 
  f_check = from_red_black(f_red, f_black); 

  u_red, u_black = sor(u_red, u_black, f_red, f_black, h, omega);

  u = from_red_black(u_red, u_black);
  return u;
}

double[n, n] v_cycle(double[n, n] F, double h)
{
  U = {[i, j] -> 0d | [i, j] < [n, n]};
  /* TODO: residual is not necessarily periodic so this gives the incorrect
     results on the boundary. */
  if (n <= 128) {
    pi    = 4d * atan(1d);
    omega = 2d / (1d + sin(pi * h));
    error = 1e308;
    i = 0;
    while (error > 1e-3 && i < 10000) {
      U = sor(U, F, h, omega);
      if (i % 100 == 0) {
        error = L2(U - F) / L2(F);
      }
      i++;
    }
  } else {
    for (i = 0; i < 3; i++) {
      U = sor(U, F, h, 2d / 3d);
    }
  
    r = F - five_point(U, h);
    r2h = restrict(r);
    error2h = v_cycle(-r2h, 2d * h);
    error = prolongate(error2h);
    U = U - error;
  
    for (i = 0; i < 3; i++) {
      U = sor(U, F, h, 2d / 3d);
    }
  }

  return U;
}

int main()
{
  pi = 4d * atan(1d);
  a = 0d;
  b = 2d * pi;
  n = 16384;
  h = (b - a) / tod(n);

  F = {[i, j] -> f(a + tod(i) * h, a + tod(j) * h)
              | [i, j] < [n, n]};
  Utrue = {[i, j] -> u(a + tod(i) * h, a + tod(j) * h)
                  | [i, j] < [n, n]};

  t = 0;
  U = {[i, j] -> 0d | [i, j] < [n, n]};
  for (t = 0; t < 5; t++) {
    r = F - five_point(U, h);
    U = U + v_cycle(r, h);
    printf("L2(r) = %e\n", L2(r));
  }

  return 0;
}
